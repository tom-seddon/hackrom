; --------------------------------
; Recover - recovers basic programs
;           from 'Bad program' errors
; --------------------------------

recoverpa:
                LDA	$18
                STA	recover_basptr+1
                LDA	#1
                STA	recover_basptr
reclp1:
                LDY	#0
                LDA	(recover_basptr),Y
                BMI	endprog
                INY
                INY
                LDA	(recover_basptr),Y
                TAY
                DEY
                LDA	(recover_basptr),Y
                TAX
                INY
                TYA
                CPX	#$0D
                BEQ	noproblem
                JSR	cure
                BCS	endprog
noproblem:
                CLC
                ADC	recover_basptr
                STA	recover_basptr
                BCC	nobasinc
                INC	recover_basptr+1
nobasinc:
                JMP	reclp1
endprog:
                RTS
cure:
                LDY	#2
reclp2:
                LDA	(recover_basptr),Y
                CMP	#$0D
                BEQ	found0D
                INY
                BNE	reclp2
                LDY	#0
                LDA	#$FF
                STA	(recover_basptr),Y
                SEC
                RTS
found0D:
                INY
                TYA
                LDY	#2
                STA	(recover_basptr),Y
                CLC
                RTS
